plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

// Dependency Versions
def jda = "v5.0.0-beta.6"
def lombok = "1.18.22"
def jetbrains = "24.0.1"
def framework = "1.0.0-ALPHA-9"

// Project Details
def main = "games.negative.bot.main.Main"
def id = "Bot"
def group = 'games.negative.bot'
def version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()

    // Sonatype Repository
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // Negative Games Repository
    maven { url 'https://repo.negative.games/repository/maven-public/' }
    maven { url 'https://m2.dv8tion.net/releases' }
}

dependencies {
    // Annotations
    compileOnly("org.projectlombok:lombok:$lombok")
    annotationProcessor("org.projectlombok:lombok:$lombok")

    compileOnly("org.jetbrains:annotations:$jetbrains")
    annotationProcessor("org.jetbrains:annotations:$jetbrains")

    // JDA
    implementation("net.dv8tion:JDA:$jda")

    // Framework / Backend
    implementation("games.negative.framework:discord-framework:$framework")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
}

tasks {
    build {
        dependsOn(shadowJar)
    }
}

shadowJar {
    archiveBaseName.set("${id}-${version}")
    archiveClassifier.set("")
    archiveVersion.set("")

    // Shade "games.negative.framework" into "games.negative.punishments.libs.framework"
    relocate "games.negative.framework", "${group}.libs.framework"
}
